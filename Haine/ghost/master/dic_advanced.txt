＃システム--------------------------------------------------------------------
＊OnNotifySelfInfo
＄現在のシェル	（R3）
＄現在のバルーン	（R5）

＃選択肢--------------------------------------------------------------------

＊OnChoiceSelectEx
＞（R1）	!（is_empty,（R2））

＃インプットボックス--------------------------------------------------------------------

＊OnUserInput
＞（R0）を空打ちされた	（is_empty,（R1））
（set,（R0）,（R1））
＞（R0）を入力した

＊OnUserInputCancel
＞（R0）を空打ちされた	（compare,（R1）,close）

＊名前入力
\0・\1（10000）\![open,inputbox,ユーザ]
＃\0・（10000）\1（10000）\![open,inputbox,ユーザ]

＊ユーザを入力した	（初回起動済）
\0・（11129）（ユーザ）、でいいのね？\nええ、これからはそう呼ぶわ。\x\c\![raise,ユーザ設定,呼ばれ方設定後]

＊ユーザを入力した	!（初回起動済）
＞名乗り後

＊ユーザを空打ちされた
＞ユーザを入力した	（初回起動済）
＄ユーザ	ユーザ
＞名乗り後

＃Web拍手------------------------------------------------------------------

＊OnWebClap
\![open,inputbox,拍手内容,,--limit=1000]φ
\_q拍手メッセージが作者に送信されますφ。
バグ報告や要望φ、感想はこちらにお願いします。

＊拍手内容を入力した
\![execute,http-post,http://clap.webclap.com/clap.php?id=aphanite&page_id=Haine,--param=message_body="（拍手内容）",--async=webclap]

＊拍手内容を空打ちされた
\_q拍手メッセージが空ですφ。\n送信は中止されました

＊拍手送信完了
\_q「（拍手内容）」\n拍手メッセージを送信しました。
＄拍手内容	

＊拍手送信失敗
\_q送信に失敗しました。
（R4）

＊OnExecuteHTTPComplete
＞拍手送信完了	（R1）==webclap

＊OnExecuteHTTPFailure
＞拍手送信失敗	（R1）==webclap

＃アンカー--------------------------------------------------------------------

＊OnAnchorSelectEx
＞（R1）
（when､（count（b1）（R1）（b1）http）､
	\j[（R1）]､
	\1\_q「（R1）」は未実装のアンカーです
）
＄訪問先url	

＃シェル、バルーン--------------------------------------------------------------------

＊OnShellChanging
＞シェル「（R0）」に変更前
\0・（11113）これを着ればいいのね？

＊OnShellChanged
＞シェル「（R0）」に変更後
\0・（11129）新鮮な気分ね。

＊OnShellChanged
＞シェル「（R0）」に変更後
\0・（11124）……どうかしら。

＊シェル「master」に変更前
\0・（11111）もとの格好がいいの？

＊シェル「master」に変更後
\0・（11111）

＊OnBalloonChange
＞バルーン「（R0）」に変更後

＊バルーン「霧の郊外にて」に変更	（現在地）==居間
\0（10000）・ \1居間。\nテーブルと椅子。\n向かい側に座るハイネが微笑んだ。\0（11127）

＊バルーン「霧の郊外にて」に変更	（現在地）==庭
\0（10000）・ \1庭。\n花と土の香りが漂っている。\0（11119）

＊バルーン「霧の郊外にて」に変更	（現在地）==書斎
\0（10000）・ \1書斎。\n飾り気のない小さな椅子が彼女の定位置だ。（11118）

＃開発関係--------------------------------------------------------------------

＊OnUpdatedataCreating

＊OnUpdatedataCreated
\_qアップデートファイル作成完了

＊OnNarCreating

＊OnNarCreated
\_qNarアーカイブ作成完了


＃専用バルーン関係--------------------------------------------------------------------

＠現在地バルーン
（when,（compare（b1）「（現在のバルーン）」（b1）「霧の郊外にて」）,
	（iflist、（現在地）==、
		庭、\b[0]、
		書斎、\b[4]、
		居間、\b[6]、
		\b[8]
	）
）

＠庭バルーン
（when,（compare（b1）「（現在のバルーン）」（b1）「霧の郊外にて」）,\b[0]）

＠書斎バルーン
（when,（compare（b1）「（現在のバルーン）」（b1）「霧の郊外にて」）,\b[4]）

＠居間バルーン
（when,（compare（b1）「（現在のバルーン）」（b1）「霧の郊外にて」）,\b[6]）

＠無地バルーン
（when,（compare（b1）「（現在のバルーン）」（b1）「霧の郊外にて」）,\b[2]）


＃デバッグ（更新前に必ずコメントアウトすること）--------------------------------------------------------------------

＊OnKeyPress
＞（Ｒ０）キー

＊aキー
＞OnFirstBoot

＊rキー
（set,リロードフラグ,1）\![change,ghost,ハイネ]

＊oキー
（OnSurfaceRestore）

＊tキー
（）

＊iキー
＞OnBoot

＊cキー
＞OnClose

＊vキー
\![open,communicatebox]

＊qキー
（直前キーワード）
（割り込み判定）
＃（set,庭訪問済み,0）（set,書斎訪問済み,0）訪問済フラグリセット

＊pキー
トーク総数　（calc,（単語群「書斎トーク」の数）+（単語群「居間トーク」の数）+（単語群「庭トーク」の数）+（単語群「共通トーク」の数））
触り反応総数　（calc,
	（単語群「headなでられ」の数）+（単語群「headつつかれ」の数）+（単語群「headころころ」の数）+（単語群「headほいくり」の数）+
	（単語群「handなでられ」の数）+（単語群「handつつかれ」の数）+（単語群「handころころ」の数）+（単語群「handほいくり」の数）+
	（単語群「shoulderなでられ」の数）+（単語群「shoulderつつかれ」の数）+（単語群「shoulderころころ」の数）+（単語群「shoulderほいくり」の数）+
	（単語群「faceなでられ」の数）+（単語群「faceつつかれ」の数）+（単語群「faceころころ」の数）+（単語群「faceほいくり」の数）+
	（単語群「hairなでられ」の数）+（単語群「hairつつかれ」の数）+（単語群「hairころころ」の数）+（単語群「hairほいくり」の数）
）
